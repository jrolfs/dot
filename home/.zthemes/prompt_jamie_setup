#!/usr/bin/env zsh


#
# Eventually this will just print out a bike shed
#
# Authors:
#   Jamie Rolfs <jamie@rolfs.sh>
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <jamie.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_jamie_calculate_truncation {
  local zero='%([BSUbfksu]|([FK]|){*})'
  local formatted=${(S%%)${(e)_prompt_jamie_first_line}//$~zero}
  local length=${#formatted}
  local overflow=$(($COLUMNS - $length))
  local git_length=${#${(S%%)_prompt_jamie_git//$~zero}}

  if [[ $overflow -lt 0 ]]; then
    (( _prompt_jamie_truncate = $git_length + $overflow ))
  fi
}

#
# Prompt hook

function prompt_jamie_precmd {
  if (( $+functions[node-info] )); then
    node-info
  fi

  if (( $+functions[python-info] )); then
    python-info
  fi

  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

  # Kill the old process of slow commands if it is still running.
  async_flush_jobs async_jamie_git

  # Handle updating git data. We also clear the git prompt data if we're in a
  # different git root now.
  if (( $+functions[git-dir] )); then
    local new_git_root="$(git-dir 2> /dev/null)"

    if [[ $new_git_root != $_jamie_current_git_root ]]; then
      prompt_jamie_reset_git

      _jamie_current_git_root=$new_git_root
    fi
  fi

  prompt_jamie_calculate_truncation

  # Compute slow commands in the background.
  async_job async_jamie_git prompt_jamie_async_git "$PWD"
 }

#
# Git info asynchronous callback

function prompt_jamie_git_info {
  # We can safely split on ':' because it isn't allowed in ref names
  IFS=':' read _git_prompt _git_prompt_right <<<"$3"

  if [[ -z "$_git_prompt" ]]; then
    # No git target detected, flush the git fragment and redisplay the prompt
    if [[ -n "$_prompt_jamie_git" ]]; then
      prompt_jamie_set_git '' ''

      zle && zle reset-prompt
    fi
  else
    # Git target detected, update the git fragment and redisplay the prompt
    prompt_jamie_set_git "${_git_prompt}" "${_git_prompt_right}"

    zle && zle reset-prompt
  fi
}

#
# Git information

function prompt_jamie_async_git {
  cd -q "$1"

  if (( $+functions[git-info] )); then
    git-info
    print "${git_info[prompt]}:${git_info[rprompt]}"
  fi
}

function prompt_jamie_set_git {
  _prompt_jamie_git=$1
  _prompt_jamie_git_right=''

  prompt_jamie_calculate_truncation

  # Replace any numbers with subscript

  local right_prompt="$2"
  local unicode_numbers=(â‚€ â‚ â‚‚ â‚ƒ â‚„ â‚… â‚† â‚‡ â‚ˆ â‚‰)
  local in_curlies=false

  for i in $(seq 1 ${#right_prompt}); do
    local character=${right_prompt:$i-1:1}

    if [ "$in_curlies" = true ]; then
      if [ "$character" = "}" ]; then
        in_curlies=false
      fi

      _prompt_jamie_git_right+=$character
      continue
    elif [ "$character" = "{" ]; then
      _prompt_jamie_git_right+=$character
      in_curlies=true
      continue
    fi

    if [[ $character =~ [0-9] ]]; then
      character=$unicode_numbers[$character+1]
    fi

    _prompt_jamie_git_right+=$character
  done
}

function prompt_jamie_reset_git {
  prompt_jamie_set_git '...' '...'
}

function prompt_jamie_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  prompt_opts=(cr percent subst)

  _prompt_jamie_precmd_async_pid=0
  _prompt_jamie_precmd_async_data=$(mktemp "${TMPDIR:-/tmp}/jamie-prompt-async-XXXXXXXXXX")

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz async && async

  # Add prompt hook
  add-zsh-hook precmd prompt_jamie_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_jamie_colors=(
      "%F{14}"
      "%F{16}"
      "%F{05}"
      "%F{13}"
      "%F{02}"
    )
  else
    _prompt_jamie_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  if [[ ! -z "$VIM" ]]; then
    _prompt_jamie_colors[2]="%F{11}"
  fi

  # Set prompt options
  zstyle ':prezto:module:prompt' pwd-length 'short'

  # Set editor-info options
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format "â€¢ "
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}â™º%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format "âˆ˜ "

  # Set git-info options
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format " :%%B${_prompt_jamie_colors[2]}%s%f%%b"
  zstyle ':prezto:module:git:info:added' format " %%B%${_prompt_jamie_colors[5]}ï•%a%f%%b"
  zstyle ':prezto:module:git:info:ahead' format " %%B${_prompt_jamie_colors[2]}ï‚ª%A%f%%b"
  zstyle ':prezto:module:git:info:behind' format " %%B${_prompt_jamie_colors[2]}ï‚«%B%f%%b"
  zstyle ':prezto:module:git:info:branch' format "${_prompt_jamie_colors[1]}%$_prompt_jamie_truncate<...<%b%<<%f"
  zstyle ':prezto:module:git:info:commit' format ":%${_prompt_jamie_colors[5]}%.7c%f"
  zstyle ':prezto:module:git:info:deleted' format " %%B${_prompt_jamie_colors[4]}ï–%d%f%%b"
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{blue}ïš%m%f%%b'
  zstyle ':prezto:module:git:info:position' format ":${_prompt_jamie_colors[4]}%p%f"
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{magenta}ï‚©%r%f%%b'
  zstyle ':prezto:module:git:info:stashed' format " %%B${_prompt_jamie_colors[1]}ï˜%S%f%%b"
  zstyle ':prezto:module:git:info:unmerged' format " %%B${_prompt_jamie_colors[2]}ïª%U%f%%b"
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{white}ï™%u%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '%b' \
    'rprompt' '%A%B%a%d%m%r%U%u%S'

  # Set node-info options
  zstyle ':prezto:module:node:info:version' format '%F{green}îœ˜ %v%f'

  # Set python-info options
  zstyle ':prezto:module:python:info:virtualenv' format '%F{green}îœ¼ %v%f'

  # Set ruby-info options
  zstyle ':prezto:module:ruby:info:version' format '%F{red}îž‘ %v%f'

  # Set syntax-highlighting options
  zstyle ':prezto:module:syntax-highlighting' highlighters \
    'main' \
    'brackets' \
    'pattern' \
    'cursor' \
    'root'

  zstyle ':prezto:module:syntax-highlighting' styles \
    'builtin' 'bg=blue' \
    'command' 'bg=blue' \
    'function' 'bg=blue'

  zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}

  # Get the async worker set up
  async_start_worker async_jamie_git -n
  async_register_callback async_jamie_git prompt_jamie_git_info

  _jamie_current_git_root=''


  #
  # Prompt

  prompt_jamie_reset_git

  _prompt_jamie_first_line='
Î» ${_prompt_jamie_colors[3]}%n%f at \
${_prompt_jamie_colors[2]}%m%f in \
${_prompt_jamie_colors[5]}%~%f \
$([ $_prompt_jamie_git ] && echo -e "ï˜ $_prompt_jamie_git")'

  _prompt_jamie_second_line='
$editor_info[keymap]'

  _prompt_jamie_right='$([ $_prompt_jamie_git_right ] && echo -e "îœ¨ $_prompt_jamie_git_right ã€œ ") \
$node_info[version] Â· \
$ruby_info[version]\
%(?:: â€¢ %F{yellow}ï€%f)'

  PROMPT=${_prompt_jamie_first_line}
  PROMPT+=${_prompt_jamie_second_line}

  RPROMPT="$_prompt_jamie_right"

  SPROMPT='%F{red}%R%f âžŸ %F{green}%r%f ðŸ¤”  ? '
}

prompt_jamie_setup "$@"
